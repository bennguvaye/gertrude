#define GertrudeVersion "@VERSION@"

[Setup]
AppName=Gertrude
AppVerName=Gertrude {#GertrudeVersion}
AppVersion={#GertrudeVersion}
AppPublisher=Bertrand Songis
AppPublisherURL=https://www.gertrude-logiciel.org
AppSupportURL=https://www.gertrude-logiciel.org
AppUpdatesURL=https://www.gertrude-logiciel.org
VersionInfoVersion={#GertrudeVersion}
VersionInfoCompany=Bertrand Songis
VersionInfoProductName=Gertrude
AppCopyright=Copyright © 2005-2020 - Bertrand Songis

DefaultDirName={pf}\Gertrude
DefaultGroupName=Gertrude

OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes

WizardImageBackColor=clWhite
SetupIconFile=bitmaps_dist\gertrude.ico
WizardImageFile=bitmaps_dist\setup_gertrude.bmp
WizardSmallImageFile=bitmaps_dist\setup_gertrude_mini.bmp
UninstallDisplayIcon={app}\gertrude.exe

[Messages]
BeveledLabel=Gertrude

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Languages]
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[InstallDelete]
Type: files; Name: "{app}\*.dll"
Type: files; Name: "{app}\*.py"
Type: files; Name: "{app}\*.pyw"
Type: files; Name: "{app}\*.pyc"
Type: files; Name: "{app}\*.pyd"
Type: files; Name: "{app}\*.manifest"
Type: files; Name: "{app}\demo.db"
Type: files; Name: "{app}\*.log"

[Dirs]
Name: "{app}"; Permissions:users-modify   

[Files]
; ---> Gertrude directory
Source: "*.py";                                  DestDir: "{app}";                     Flags: ignoreversion
Source: "*.pyw";                                 DestDir: "{app}";                     Flags: ignoreversion
Source: "*.ini.dist";                            DestDir: "{app}";                     Flags: ignoreversion
Source: "demo.db";                               DestDir: "{app}";                     Flags: ignoreversion

; ---> Subdirectories
Source: "WinPython-32bit-2.7.13.1Zero\*";        DestDir: "{app}\WinPython-32bit-2.7.13.1Zero";        Flags: ignoreversion recursesubdirs createallsubdirs
Source: "generation\*.py";                       DestDir: "{app}\generation";                          Flags: ignoreversion recursesubdirs createallsubdirs
Source: "bitmaps_dist\*";                        DestDir: "{app}\bitmaps_dist";                        Flags: ignoreversion recursesubdirs createallsubdirs
Source: "bitmaps_dist\pictos\*";                 DestDir: "{app}\bitmaps_dist\pictos";                 Flags: ignoreversion recursesubdirs createallsubdirs
Source: "templates_dist\*";                      DestDir: "{app}\templates_dist";                      Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\Gertrude";                        Filename: "{app}\WinPython-32bit-2.7.13.1Zero\python-2.7.13\pythonw.exe"; WorkingDir: "{app}"; Parameters: """{app}\gertrude.pyw"""; IconFilename: "{app}\bitmaps_dist\gertrude.ico"
Name: "{userdesktop}\Gertrude";                  Filename: "{app}\WinPython-32bit-2.7.13.1Zero\python-2.7.13\pythonw.exe"; WorkingDir: "{app}"; Parameters: """{app}\gertrude.pyw"""; IconFilename: "{app}\bitmaps_dist\gertrude.ico"

[Run]
Filename: "{app}\WinPython-32bit-2.7.13.1Zero\python-2.7.13\pythonw.exe"; Description: "Démarrer Gertrude"; WorkingDir: "{app}"; Parameters: """{app}\gertrude.pyw"""; Flags: nowait postinstall skipifsilent

[UninstallDelete]
; uninstallation removes the compiled python files
Type: files; Name: "{app}\*.pyc"
